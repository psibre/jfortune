plugins {
    id 'groovy'
    id 'application'
    id 'de.undercouch.download' version '3.2.0'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

mainClassName = 'JFortune'

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.10'
    testCompile 'junit:junit:4.10'
}

import groovy.json.JsonBuilder

task processFortunes {
    def destFile = file("$buildDir/fortunes.json")
    outputs.file destFile
    doLast {
        download {
            src 'https://raw.githubusercontent.com/ruanyf/fortunes/master/data/fortunes'
            dest temporaryDir
            overwrite false
        }
        def fortunes = file("$temporaryDir/fortunes").text.split(/\n?%\n?/)
        destFile.text = new JsonBuilder(fortunes).toPrettyString()
    }
}

processResources {
    from processFortunes
}
